# Unreal Engine 4 - Minecraft Dungeons (*WindowsNoEditor.pak) (script 0.0.1)
# Script for QuickBMS http://quickbms.aluigi.org

math NO_TAIL_INFO = 0   # set it to 1 for archives with corrupt/missing tail information

quickbmsver "0.8.4"

math TOC_FILE = 0

# Dungeons Beta AES Key
# set AES_KEY binary "0x478762BAEC53557F2BB59292B1341F1ADA335163D1335CE338A3A02B78F14271"

# Dungeons 1.2.0.0 - 1.7.3.0 AES Key
# set AES_KEY binary "0x90F270A4EA6DE0E3BABDC4C8BDC04F46FA5B9087BE8FE76AA859C93C17D04661"

# Dungeons 1.8.0.0 AES Key
# set AES_KEY binary "0x0634AF9A409BA6D24FC17B6872124A91F3DBC4C3986F639A5E29C1BB10B2A099"

# Dungeons 1.8.6.0 AES Key
# set AES_KEY binary "0xA031D8DC72D498FF3856CF3B6E7FCA0DF09D59E6C75DCA8A9FBE152C7982C4A2"

# Dungeons 1.8.8.0 AES Key
# set AES_KEY binary "0x8F80D8A467F135AC43BB1324861EF5D91316AD954A09D4B3499A8CCA8AE70AB7"

# Dungeons 1.9.1.0 AES Key
# set AES_KEY binary "0x0A01138AC7553ECEA7E9E257B5A45FA6665679D360B270278AE5D5367B172E40"

# Dungeons 1.10.2.0 AES Key
# set AES_KEY binary "0xCCB6409075433E2BE4CC249C48D9B5350741A9F9399A51B9E3B6742EF13947FD"

# Dungeons 1.10.3.0 AES Key
# set AES_KEY binary "0xC4D2F53C2244E99D44CB125F2885A3007843492EB8814704D08A6F8D748E3055"

# Dungeons 1.11.1.0 AES Key
# set AES_KEY binary "0x2B7F564A46599987F86C3784BC9FA21254F0802777D55955567EB0AAEC2CB159"

# Dungeons 1.12.0.0 AES Key
# set AES_KEY binary "0x72FA28089D1E511139E4EA804A6577B37070940DC43D72D62D2C889A728D9620"

# Dungeons 1.13.1.0 AES Key
# set AES_KEY binary "0x6B2EE0E1CB868A2297733A6741D5C81B6EA422A4A7320A3FC5E44ED6CEBDB595"

# Dungeons 1.14.1.0 AES Key
# set AES_KEY binary "0x16D28F03446CB9E15665F11EF5A0D6820A6483C083CA6DF1AE9370D197ACB588"

# Dungeons 1.15.0.0 AES Key
# set AES_KEY binary "0x311859D894921574C4EB03F9B55C96982D323D3C8336A826FAB1941C74B98945"

# Dungeons 1.15.1.0 AES Key
# set AES_KEY binary "0xE06CE229E7A7051CED9969D49740A5C5C6A8C8F2F1B04186E589BE01D026140E"

# Dungeons 1.16.2.0 AES Key
# set AES_KEY binary "0x0566BFE22AA8544F59B022D8E171DEA5EC4200BB47109AA781D583B7B01226E8"

# Dungeons 1.16.3.0 AES Key
# set AES_KEY binary "0xAE74A59361EFFB891D252EB410E13ED985D1941010F664A7570A774B2C9D0D6F"

# Dungeons 1.17.0.0 AES Key
set AES_KEY binary "0x7D5F892ECEBFA53CC22001DF48B871D51C0DF7C54CE41933BFB285219829B3A8"

string AES_KEY << 2 # Removes the 0x
string AES_KEY h AES_KEY
math AES_KEY_IS_SET = 1 # makes the check later return the AES_KEY variable instead of asking

math ALTERNATIVE_MODE = 0

get ARCHIVE_NAME basename
get ARCHIVE_PATH FILEPATH

# Dungeons is 8
math VERSION = 0

math CHUNK_OFFSET_ABSOLUTE = -1 # default, enabled

# 1 = HIT
# 4 = Dungeons?
math WORKAROUND = 0

if NO_TAIL_INFO != 0
    #pak file doesn't have tail information, OFFSET is set to the file size
    get OFFSET asize
    math ALTERNATIVE_MODE = 1
else
    #pak file has tail information
    #go to position 172 bytes before the file end
    goto -0xac # for version 8
	savepos MAGIC_OFF
	get MAGIC long  #idstring "\xe1\x12\x6f\x5a" # 0x5a6f12e1
	
	if MAGIC != 0x5a6f12e1
		goto -0x2c # previous versions 3-6

        #older pak file version, go to 44 bytes before the file end
		savepos MAGIC_OFF
		get MAGIC long
	else
		math WORKAROUND = 4
	endif
    
    if MAGIC == 0
        #weird pak format, magic starts at 204 bytes before the file end
        goto -0xcc  # Fortnite iOS
        savepos MAGIC_OFF
        get MAGIC long
        math WORKAROUND = 3
    endif

    endian guess MAGIC      # 0x5a6f12e1:le, 0xe1126f5a:be
    if MAGIC == 0x1233a     # HIT game
        math WORKAROUND = 1
    elif MAGIC == 0x1066a   # OverHit
        math WORKAROUND = 1
    elif MAGIC == 0x13aed   # OverHit
        math WORKAROUND = 1
    elif ARCHIVE_NAME & "OverHit"
        math WORKAROUND = 1
    elif ARCHIVE_NAME == "0"    # HIT
        math WORKAROUND = 1
    elif ARCHIVE_PATH & "Gears of War 4"
        math WORKAROUND = 2
    endif

    #tail information is so far known to contain:
    # - magic string (32bit)
    # - pak version (32bit)
    # - offset variable (64 bits)
    # - size variable (64 bits)
    # - hash (20 bytes)

    get VERSION long
    endian guess VERSION
    get OFFSET longlong
    get SIZE longlong
    getdstring HASH 20
    if WORKAROUND == 1
        math VERSION = 3
    endif

    if VERSION >= 3
        #the pak file can be encrypted, a byte before the tail information has info
        goto MAGIC_OFF
        goto -1 0 SEEK_CUR
        get ENCRYPTED byte
        if ENCRYPTED != 0
            #the pak is encrypted
            callfunction SET_AES_KEY 1
            log MEMORY_FILE10 OFFSET SIZE
            encryption "" ""
            math TOC_FILE = -10 # MEMORY_FILE10 is -10
        endif
    endif

    goto OFFSET # don't use TOC_FILE
    callfunction GET_NAME 1
    if ALTERNATIVE_MODE == 0
        set MOUNTING_POINT filepath NAME
        String MOUNTING_POINT | "../../../"
        String MOUNTING_POINT | "../../.."
        print "Mounting point: %MOUNTING_POINT%"
    else 
        print "WRONG AES KEY!"
        math TMP_EXC = 0
        math TMP_EXC / 0
    endif
endif

if ALTERNATIVE_MODE == 0
    get FILES long TOC_FILE
else
    math FILES = 0x7fffffff
    math MAX_OFF = OFFSET
    goto 0
    set NAME string ""
endif

math CHUNK_SIZE = 0x10000   # just in case...
#print "Files: %FILES%"
for i = 0 < FILES
    if ALTERNATIVE_MODE == 0
        callfunction GET_NAME 1
        if NAMESZ == 0  # lame work-around for Conan Exiles
            callfunction GET_NAME 1
        endif
    endif
    if MOUNTING_POINT != ""
        #apply the mounting point
        String RAWNAME = NAME
        String NAME = MOUNTING_POINT
        String NAME + "/"
        String NAME + RAWNAME
    endif
    savepos TMP_OFF TOC_FILE

    get OFFSET longlong TOC_FILE
    if WORKAROUND == 2
        get ZSIZE long TOC_FILE
        get SIZE long TOC_FILE
        get ZIP byte TOC_FILE
	elif WORKAROUND == 4
		get ZSIZE longlong TOC_FILE
        get SIZE longlong TOC_FILE
        get ZIP byte TOC_FILE
    else
        get ZSIZE longlong TOC_FILE
        get SIZE longlong TOC_FILE
        get ZIP long TOC_FILE
    endif
    if WORKAROUND == 1
        getdstring HASH 20 TOC_FILE
    elif VERSION <= 1
        get TSTAMP longlong TOC_FILE
    endif
    if WORKAROUND == 2
    else
        getdstring HASH 20 TOC_FILE
    endif
    math CHUNKS = 0
    math ENCRYPTED = 0
    if VERSION >= 3
        if ZIP != 0
            get CHUNKS long TOC_FILE
            for x = 0 < CHUNKS
                get CHUNK_OFFSET longlong TOC_FILE
                get CHUNK_END_OFFSET longlong TOC_FILE
                putarray 0 x CHUNK_OFFSET
                putarray 1 x CHUNK_END_OFFSET
            next x
        endif
        if WORKAROUND == 2
        else
            get ENCRYPTED byte TOC_FILE
        endif
        get CHUNK_SIZE long TOC_FILE
    endif
    if WORKAROUND == 1
        if ARCHIVE_NAME == "0"  # HIT game only!
            math ENCRYPTED = 0
        endif
    endif
    if ALTERNATIVE_MODE != 0
        savepos TMP_OFF TOC_FILE
        math OFFSET + TMP_OFF
    endif

    #comtype copy    # for AES, do not enable or quickbms 0.9.2 will fail
      if ZIP == 3 # JoyfunRPG / Faith of Danschant
        comtype oodle
    elif ZIP & 1
        comtype zlib
    elif ZIP & 2
        comtype gzip
    elif ZIP & 4
        comtype snappy
        if VERSION >= 6
            comtype oodle
        endif
        if WORKAROUND == 2
            comtype lz4
        endif
    elif ZIP & 0x10
        comtype oodle
    else
        math ZIP = 0
    endif

    if CHUNKS > 0
        log NAME 0 0 # this should be the file writing one
        append
        math TMP_SIZE = SIZE
		if CHUNK_OFFSET_ABSOLUTE < 0 && OFFSET != 0
			getarray CHUNK_OFFSET 0 0
			if CHUNK_OFFSET u< OFFSET || VERSION >= 5
				math CHUNK_OFFSET_ABSOLUTE = 0
			else
				math CHUNK_OFFSET_ABSOLUTE = 1
			endif
		endif
        for x = 0 < CHUNKS
            getarray CHUNK_OFFSET 0 x
            getarray CHUNK_END_OFFSET 1 x
            math CHUNK_ZSIZE = CHUNK_END_OFFSET
            math CHUNK_ZSIZE - CHUNK_OFFSET
            math CHUNK_XSIZE = CHUNK_ZSIZE
            if ENCRYPTED != 0
                callfunction SET_AES_KEY 1
                math CHUNK_XSIZE x 16
            endif
            if TMP_SIZE u< CHUNK_SIZE
                math CHUNK_SIZE = TMP_SIZE
            endif
            if CHUNK_OFFSET_ABSOLUTE == 0
                math CHUNK_OFFSET + OFFSET
            endif
            if ZIP == 0
                log NAME CHUNK_OFFSET CHUNK_SIZE 0 CHUNK_XSIZE
            else
                clog NAME CHUNK_OFFSET CHUNK_ZSIZE CHUNK_SIZE 0 CHUNK_XSIZE
            endif
            math TMP_SIZE - CHUNK_SIZE
        next x
        append
    else
        # the file offset points to an entry containing
        # the "same" OFFSET ZSIZE SIZE ZIP HASH ZERO fields,
        # just an additional backup... so let's skip them
        savepos BASE_OFF TOC_FILE
        math BASE_OFF - TMP_OFF
        math OFFSET + BASE_OFF
        math XSIZE = ZSIZE
        if ENCRYPTED != 0
            callfunction SET_AES_KEY 1
            math XSIZE x 16
        endif
        if ZIP == 0
            log NAME OFFSET SIZE 0 XSIZE
        else
            clog NAME OFFSET ZSIZE SIZE 0 XSIZE
        endif
    endif
    encryption "" ""

    if ALTERNATIVE_MODE != 0
        math OFFSET + ZSIZE
        goto OFFSET
        if OFFSET == MAX_OFF
            break
        endif
        if VERSION >= 4
            padding 0x800   # necessary for WitchIt
        endif
    endif
next i

startfunction SET_AES_KEY_ASK
    math AES_KEY_IS_SET = 1
    print "The archive is encrypted, select the number of the key to use or type yours:"
    for z = 0
        getarray KEY 10 z
        if KEY == ""
            break
        endif
        print "%z%: %KEY%"
    next z
        print "%z%: press RETURN for no encryption (Lineage 2 Revolution)"
    set KEY unknown "???"
    strlen TMP KEY
    if KEY == ""
        math AES_KEY_IS_SET = -1
        set AES_KEY string "no key, encryption disabled"
    elif TMP <= 2
        getarray AES_KEY 10 KEY
        if AES_KEY strncmp "0x"
            string AES_KEY << 2
            string AES_KEY h AES_KEY
        endif
    elif KEY strncmp "0x"
        string KEY << 2
        string AES_KEY h KEY
    else
        set AES_KEY binary KEY
    endif
    print "KEY: %AES_KEY%"
endfunction

startfunction SET_AES_KEY
    if AES_KEY_IS_SET == 0
        callfunction SET_AES_KEY_ASK 1
    endif
    if AES_KEY_IS_SET > 0
        encryption aes AES_KEY "" 0 32
    endif
endfunction

startfunction GET_NAME
    get NAMESZ signed_long TOC_FILE
    if NAMESZ >= 0
        if NAMESZ > 0x200
            math ALTERNATIVE_MODE = 1
        else
            getdstring NAME NAMESZ TOC_FILE
        endif
    else
        math NAMESZ n NAMESZ
        if NAMESZ > 0x200
            math ALTERNATIVE_MODE = 1
        else
            math NAMESZ * 2
            getdstring NAME NAMESZ TOC_FILE
            set NAME unicode NAME
        endif
    endif
endfunction
